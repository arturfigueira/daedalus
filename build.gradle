plugins {
    id 'java'
    id "org.sonarqube" version "3.3"
    id 'org.javamodularity.moduleplugin' version '1.8.7' apply false
}

group 'com.daedalus'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "arturfigueira_daedalus"
        property "sonar.organization", "arturfigueira"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: "org.javamodularity.moduleplugin"

    sourceCompatibility=11
    targetCompatibility=11

    dependencies {
        //Spock
        implementation "org.codehaus.groovy:groovy:3.0.8"
        testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0")
        testImplementation "org.spockframework:spock-core"
        testImplementation "org.hamcrest:hamcrest-core:2.2"
        testImplementation 'com.athaydes:spock-reports:2.0-groovy-3.0'

        implementation 'com.google.code.gson:gson:2.8.7'

        //Lombok
        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    }

    test {
        useJUnitPlatform()
        exclude "**/*ITSpec*", "**/*IntSpec*"
        finalizedBy jacocoTestReport // report is always generated after tests run

    }

    task integrationTest(type: Test) {
        useJUnitPlatform()
        description = "Execute integration tests."
        group = "verification"
        include "**/*ITSpec*", "**/*IntSpec*"
        mustRunAfter(tasks.named('test'))
        testLogging {
            events 'FAILED', 'SKIPPED'
        }
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
            html.required = true
        }
    }

    jacocoTestReport.dependsOn test

    check.dependsOn integrationTest
}